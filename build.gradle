import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.26.0'
}

group = 'ir.darkdeveloper'
version = '1.1.3'
sourceCompatibility = '19'
targetCompatibility = '19'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


repositories {
    mavenCentral()
}

application {
    mainClass = 'ir.darkdeveloper.jbookfinder.AppLauncher'
    mainModule = 'ir.darkdeveloper.jbookfinder'
}

javafx {
    version = "19.0.2.1"
    modules = ['javafx.controls', 'javafx.fxml', "javafx.graphics"]
}


dependencies {
    implementation 'org.controlsfx:controlsfx:11.1.2'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation 'org.xerial:sqlite-jdbc:3.40.0.0'
    implementation 'io.projectreactor:reactor-core:3.4.24'
    implementation 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}


tasks.named('test') {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip") as RegularFile
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'JBookFinder'
    }
    jpackage {
        if (OperatingSystem.current().windows) {
            appVersion = version
            installerName = 'JBookFinder'
            installerOptions = ['--win-menu', '--win-shortcut', '--icon', 'src/main/resources/ir/darkdeveloper/jbookfinder/icons/logo.ico']
            imageOptions = ['--icon', 'src/main/resources/ir/darkdeveloper/jbookfinder/icons/logo.ico']
        }
    }
    mergedModule {
        excludeProvides servicePattern: 'reactor.blockhound.*'
    }
}

jlinkZip {
    group = 'distribution'
}
